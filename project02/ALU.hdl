// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    
    // zx, zero the x input?
    Mux16(a=x, b[0..15]=false, sel=zx, out=xzero);
    
    // ng, negate the x input?    
    Not16(in=xzero, out=notx);
    Mux16(a=xzero, b=notx, sel=nx, out=xnegate);
    
    // zy, zero the y input?    
    Mux16(a=y, b[0..15]=false, sel=zy, out=yzero);
    
    // ny, negate the y input?
    Not16(in=yzero, out=noty);
    Mux16(a=yzero, b=noty, sel=ny, out=ynegate);
    
    // f, compute (out = x + y) or (out = x & y)?    
    And16(a=xnegate, b=ynegate, out=xandy);
    Add16(a=xnegate, b=ynegate, out=xplusy);
    Mux16(a=xandy, b=xplusy, sel=f, out=xfy);
    
    // no, negate the out output?
    Not16(in=xfy, out=negatef);
    Mux16(a=xfy, b=negatef, sel=no, out[0..7]=outfirst, out[8..15]=outlast, out[15]=out16, out=out);
    
    // zr, if (out == 0) equals 1, else 0
    Or8Way(in=outfirst, out=zr1);
    Or8Way(in=outlast, out=zr2);
    Or(a=zr1, b=zr2, out=zrtemp);
    Not(in=zrtemp, out=zr);
    
    // ng if (out < 0)  equals 1, else 0    
    Or(a=out16, b=false, out=ng);    
}